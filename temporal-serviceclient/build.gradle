apply plugin: 'com.netflix.grpc.proto-definition'

license {
    exclude "**/v1/*.java"
    exclude "gogoproto/*.java"
}

apply plugin: 'idea' // IntelliJ plugin to see files generated from protos

description = '''Temporal Workflow Java SDK'''

dependencies {
    api 'netflix.com.google.protobuf:protobuf-java-nflx'
    api 'netflix.io.grpc:grpc-protobuf-nflx'
    api 'netflix.io.grpc:grpc-stub-nflx'
    api 'netflix.io.grpc:grpc-core-nflx'
    api 'netflix.io.grpc:grpc-netty-shaded-nflx'
    api 'netflix.grpc:netflix-grpc-options-proto-definition'
    api 'netflix.com.google.protobuf:protobuf-java-util-nflx'
    api group: 'com.uber.m3', name: 'tally-core', version: '0.6.1'
    api group: 'org.slf4j', name: 'slf4j-api'
    if (!JavaVersion.current().isJava8()) {
        implementation 'javax.annotation:javax.annotation-api'
    }
    implementation 'com.google.guava:guava'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic'
    testImplementation group: 'org.mockito', name: 'mockito-core'
    testImplementation group: 'junit', name: 'junit'
}

task initSubmodules(type: Exec) {
    description = 'Initializes submodules'
    commandLine 'git', 'submodule', 'init'
}

task updateSubmodules(type: Exec) {
    dependsOn initSubmodules
    description = 'Update submodules'
    commandLine 'git', 'submodule', 'update'
}

tasks.checkProto.dependsOn('updateSubmodules')

//TODO(maxim): For some reason the following doesn't exclude proto files from the lib jar.
// But it should according to https://github.com/google/protobuf-gradle-plugin/issues/390
jar {
    exclude '**.proto'
    includeEmptyDirs false
}

// Generation version.properties for value to be included into the request header
task createProperties(dependsOn: processResources) {
    doLast {
        def subdir = new File("$buildDir/resources/main/io/temporal/")
        if (!subdir.exists()) {
            subdir.mkdirs()
        }
        new File("$buildDir/resources/main/io/temporal/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['temporal-client-version'] = getClientVersionName()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

// Needed to include generated files into the source jar
//tasks.sourcesJar.from(file("$buildDir/generated/main/java"))

task registerNamespace(type: JavaExec) {
    main = 'io.temporal.serviceclient.RegisterTestNamespace'
    classpath = sourceSets.test.runtimeClasspath
}

test.dependsOn 'registerNamespace'
